//------------------------------------------------------------------------------- Camera Shake Event
class CameraShakeEvent : ZilchEvent
{
  var Intensity : Real;
  var WorldPosition : Real3;
}

//------------------------------------------------------------------------------------- Camera Shake
// Because this script sets the translation of camera around the origin, you would want to attach
// the Camera to another object. Any logic to make the camera follow the player should be
// applied to the parent object, and not the camera itself.
//
// Another thing to note - If the Microphone component is on the Camera, you will get a bad
// doppler affect while the camera is shaking. Move the microphone to the parent object.
class CameraShake : ZilchComponent
{
  // We require a transform to apply the random position.
  [Dependency] var Transform : Transform;
  
  [Property] var Active = true;
  
  // Used to generate a random position offset. The higher the number, the more intense the shake.
  // Instead of calling a function to set the intensity, a property can be useful because you
  // can += to it to avoid over-writing a previous shake.
  [Property] var CurrentIntensity : Real = 0.0;
  
  // A scalar that determines how slowly the shake settles.
  // 0 means the shake instantly stops
  // 1 means it will shake forever
  [Property] var Dissipation : Real = 0.97;
  
  var Random : Random = new Random(5);
  
  sends ShakeCamera : CameraShakeEvent;
  
  //************************************************************************************************
  [Static] function ShakeCamera(intensity : Real, worldPosition : Real3, space : Space)
  {
    var eventToSend = CameraShakeEvent() { Intensity = intensity, WorldPosition = worldPosition};
    space.DispatchEvent(Events.ShakeCamera, eventToSend);
  }
  
  //************************************************************************************************
  function Initialize(init : CogInitializer)
  {
    // Every frame we want to update 
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    Zero.Connect(this.Space, Events.ShakeCamera, this.OnShakeCamera);
  }

  //************************************************************************************************
  function OnShakeCamera(event : CameraShakeEvent)
  {
    this.CurrentIntensity += event.Intensity;
  }
  
  //************************************************************************************************
  function OnLogicUpdate(event : UpdateEvent)
  {
    if(this.Active == false)
      return;
      
    // Generate a random position
    var pos = Real3(0);
    pos.XY = this.Random.Real2(0, this.CurrentIntensity);
    
    // If your camera just sits at the origin in your game, you don't have to anchor it
    // to another object. This line maintains the z translation of the camera to
    // push it back for frustum culling
    pos.Z = this.Owner.Transform.Translation.Z;
    
    // Set the new position
    this.Owner.Transform.Translation = pos;
    
    // Apply the dissipation
    this.CurrentIntensity *= this.Dissipation;
  }
}

//---------------------------------------------------------------------------- Shake Camera On Death
class ShakeCameraOnDeath : ZilchComponent
{
  [Property] var Intensity : Real = 0.5;
  
  //************************************************************************************************
  function Initialize(init : CogInitializer)
  {
    // Listen for an event on ourself that says we died.
    Zero.Connect(this.Owner, Events.Death, this.OnDeath);
  }

  //************************************************************************************************
  function OnDeath(e : Event)
  {
    var worldPos = this.Owner.Transform.WorldTranslation;
    CameraShake.ShakeCamera(this.Intensity, worldPos, this.Space);
  }
}

//----------------------------------------------------------------------- Shake Camera On Initialize
class ShakeCameraOnInitialize : ZilchComponent
{
  [Property] var Intensity = 1.0;
  
  //************************************************************************************************
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(init, Events.AllObjectsInitialized, this.OnAllObjectsInitialized);
  }
  
  //************************************************************************************************
  function OnAllObjectsInitialized(event : CogInitializerEvent)
  {
    var worldPos = this.Owner.Transform.WorldTranslation;
    CameraShake.ShakeCamera(this.Intensity, worldPos, this.Space);
  }
}
